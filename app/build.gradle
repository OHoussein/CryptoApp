plugins {
    id 'com.android.application'
    id "kotlin-android"
    id "kotlin-parcelize"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id 'org.jetbrains.kotlin.android'
}

apply from: "$rootDir/gradle/scripts/detekt.gradle"
apply from: "$rootDir/androidModule.gradle"
apply from: "$rootDir/dependencies.gradle"

android {
    compileSdkVersion project.compileSdkVersion
    defaultConfig {
        applicationId project.applicationId
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode project.versionCode
        versionName project.versionName
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //TO-DO signing keys
            signingConfig debug.signingConfig
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        resources.excludes.add("**/attach_hotspot_windows.dll")
        resources.excludes.add("META-INF/*")
        exclude "META-INF/licenses/**"
    }
}

allprojects {
    configurations.all {
        resolutionStrategy {
            force("org.objenesis:objenesis:2.6")
        }
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation project(path: ':data:network')
    implementation project(path: ':data:database')
    implementation project(path: ':crypto:presentation')
    implementation project(path: ':core:injection')

    //Common
    implementation core.kotlin.coroutinesCore
    implementation core.kotlin.coroutinesAndroid
    implementation core.timber

    implementation core.hilt.android
    kapt core.hilt.compiler
    kapt core.hilt.androidCompiler

    //Presentation
    implementation android.jetpack.appcompat
    implementation android.jetpack.material
    implementation android.jetpack.lifecycle
    implementation android.compose

    //Data
    implementation data.retofit
    implementation data.room.runtime

    //Debug tools
    debugImplementation debug

    //Android tests;
    androidTestImplementation androidTest.hiltTesting
    kaptAndroidTest core.hilt.androidCompiler
    androidTestImplementation androidTest.androidJunit
    androidTestImplementation androidTest.rules
    androidTestImplementation androidTest.espresso
    androidTestImplementation androidTest.mockitoAndroid
    androidTestImplementation test.junit
    androidTestImplementation test.coroutinesTest
    androidTestImplementation androidTest.composeTesting
    debugImplementation androidTest.composeTestingManifest
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation project(path: ':core:test')

    //Unit tests
    testImplementation project(path: ':core:test')
    testImplementation test.mockito
    testImplementation test.junit
    testImplementation test.archCoreTesting
    testImplementation test.coroutinesTest
}
