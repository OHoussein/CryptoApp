apply plugin: "com.github.ben-manes.versions"
apply from: "dependencies.gradle"

buildscript {
    ext {
        androidGradlePlugin = '7.3.0-beta05'
        kotlinVersion = "1.7.10"
        daggerHiltVersion = "2.43.2"
        detektVersion = "1.21.0"
        jacocoVersion = '0.8.8'
    }

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:${androidGradlePlugin}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("com.google.dagger:hilt-android-gradle-plugin:${daggerHiltVersion}")
        classpath("com.github.ben-manes:gradle-versions-plugin:0.42.0")
        classpath("io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${detektVersion}")
        classpath("io.gitlab.arturbosch.detekt:detekt-formatting:${detektVersion}")
        classpath("org.jacoco:org.jacoco.core:$jacocoVersion")
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if ('org.jacoco' == details.requested.group) {
                    details.useVersion jacocoVersion
                }
            }
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

tasks.register("clean").configure {
    delete("build")
}

tasks.register("installGitHook", Copy) {
    from(new File(rootProject.rootDir, "scripts/pre-commit.sh")) {
        rename { it.substring(0, it.lastIndexOf('.')) }
    }
    into { new File(rootProject.rootDir, ".git/hooks") }
    fileMode = 0b111_111_111
}

tasks.getByPath(":app:preBuild").dependsOn(":installGitHook")


tasks.named("dependencyUpdates").configure {
    resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}